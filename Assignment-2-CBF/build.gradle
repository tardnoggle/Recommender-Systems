apply plugin: 'java'

description = 'Assignment 3: User-User CF'
ext.lenskitVersion = '3.0-T3'

ext.userList = []
if (project.hasProperty('userId')) {
    userList = project.userId.split(/,/).toList()
}
if (userList.isEmpty()) {
    userList << 42L
}

ext.itemList = []
if (project.hasProperty('itemIds')) {
    itemList = project.itemIds.split(/,/).toList()
}
if (itemList.isEmpty()) {
    itemList = [260,153,527,588]
}


repositories {
    mavenCentral()
}

dependencies {
    compile "org.grouplens.lenskit:lenskit-core:$lenskitVersion"
    runtime "org.grouplens.lenskit:lenskit-cli:$lenskitVersion"
}

task recommend(type: JavaExec, group: 'run') {
    classpath sourceSets.main.runtimeClasspath
    main 'org.grouplens.lenskit.cli.Main'
    args '--log-file', file("$buildDir/recommend.log"), '--log-file-level', 'DEBUG'
    args 'recommend'
    args '-c', file('etc/user-user.groovy')
    args userList
}

task recommendBlended(type: JavaExec, group: 'run') {
    classpath sourceSets.main.runtimeClasspath
    main 'org.grouplens.lenskit.cli.Main'
    args '--log-file', file("$buildDir/recommend-blended.log"), '--log-file-level', 'DEBUG'
    args 'recommend'
    args '-c', file('etc/blended.groovy')
    args userList
}

task predict(type: JavaExec, group: 'run') {
    classpath sourceSets.main.runtimeClasspath
    main 'org.grouplens.lenskit.cli.Main'
    args '--log-file', file("$buildDir/predict.log"), '--log-file-level', 'DEBUG'
    args 'predict'
    args '-c', file('etc/user-user.groovy')
    if (userList.size() > 0) {
        args userList.first()
        args itemList
    }
}

task prepareSubmission(type: Zip, group: 'package') {
    archiveName = 'uu-submission.zip'
    from projectDir
    into 'uu-assignment'
    include '*.txt', '*.md'
    include 'src/**'
    include 'etc/*/'
    include 'build.gradle'
}

